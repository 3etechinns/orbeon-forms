<!--
  Copyright (C) 2011 Orbeon, Inc.

  This program is free software; you can redistribute it and/or modify it under the terms of the
  GNU Lesser General Public License as published by the Free Software Foundation; either version
  2.1 of the License, or (at your option) any later version.

  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
  without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
  See the GNU Lesser General Public License for more details.

  The full text of the license is available at http://www.gnu.org/copyleft/lesser.html
  -->
<xbl:xbl xmlns:xh="http://www.w3.org/1999/xhtml"
         xmlns:xf="http://www.w3.org/2002/xforms"
         xmlns:xs="http://www.w3.org/2001/XMLSchema"
         xmlns:ev="http://www.w3.org/2001/xml-events"
         xmlns:xxf="http://orbeon.org/oxf/xml/xforms"
         xmlns:fr="http://orbeon.org/oxf/xml/form-runner"
         xmlns:saxon="http://saxon.sf.net/"
         xmlns:xbl="http://www.w3.org/ns/xbl"
         xmlns:xxbl="http://orbeon.org/oxf/xml/xbl"
         xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
         xmlns:oxf="http://www.orbeon.com/oxf/processors"
         xmlns:exf="http://www.exforms.org/exf/1-0"
         xmlns:fb="http://orbeon.org/oxf/xml/form-builder"
         xmlns:sectionOps="java:org.orbeon.oxf.fb.SectionOps">

    <xbl:binding id="fr-section" element="fr|section" xxf:external-events="fb-set-current-section fb-update-section-label" xxbl:mode="binding lhha custom-lhha">

        <xbl:handlers>
            <xbl:handler event="fr-expand" phase="target">
                <xf:setvalue ref="instance('open')">true</xf:setvalue>
            </xbl:handler>
            <xbl:handler event="fr-collapse" phase="target">
                <xf:setvalue ref="instance('open')">false</xf:setvalue>
            </xbl:handler>
        </xbl:handlers>

        <xbl:implementation>
            <xf:model id="model">
                <xf:instance id="open">
                    <open/>
                </xf:instance>
            </xf:model>
        </xbl:implementation>

        <xbl:template xxbl:transform="oxf:xslt">
            <xsl:transform version="2.0">
                <xsl:import href="oxf:/oxf/xslt/utils/copy.xsl"/>
                <xsl:import href="oxf:/oxf/xslt/utils/xbl.xsl"/>

                <xsl:template match="/*">

                    <!-- Section configuration -->
                    <xsl:variable name="mode" select="@mode"/>
                    <xsl:variable name="ancestor-sections" select="count(ancestor::fr:section)"/><!-- TODO -->
                    <xsl:variable name="collapse" select="not(@collapse = 'false')"/>
                    <xsl:variable name="animate" select="not(@animate = 'false')"/>

                    <xsl:variable name="open" as="xs:boolean" select="$mode = 'view' or not(@open = 'false')"/>

                    <!-- Section content area -->
                    <xf:group appearance="xxf:internal" xxbl:scope="outer">
                        <xf:var name="id" xbl:attr="xbl:text=id"/>
                        <xf:var name="fb-section" value="xxf:instance('fb-form-instance')/xh:body//fr:section[@id = $id]"/>
                        <xf:var name="fb-can-do-classes" value="if (exists($fb-section)) then sectionOps:canDoClasses($fb-section) else ()"/>

                        <!-- It'd be nice if we could put the class attribute directly on xbl-fr-section -->
                        <xf:group
                            class="{{string-join((@class, $fb-can-do-classes), ' ')}}"
                            xxf:element="div">

                            <xf:group xxbl:scope="inner" appearance="xxf:internal">

                                <!-- Q: Is this the best way to get access to Form Runner resources? -->
                                <xf:var name="fr-resources"><xxf:sequence value="$fr-resources" xxbl:scope="outer"/></xf:var>

                                <!-- Whether the section is open, initialized from XSLT variable -->
                                <xf:var name="open" value="instance('open') = 'true'">
                                    <!-- TODO: get rid of the toggle once we have a data-bound switch -->
                                    <xf:toggle ev:event="xforms-value-changed" case="case-{{if (. = 'true') then 'open' else 'closed'}}"/>
                                </xf:var>

                                <xf:setvalue ev:event="xforms-enabled" ev:target="#observer" ref="instance('open')" value="'{$open}'"/>

                                <!-- Section title area: open/close button, title, help -->
                                <xsl:element name="{if ($ancestor-sections = 0) then 'xh:h2' else 'xh:h3'}">
                                    <xsl:attribute name="class" select="'fr-section-title'"/>

                                    <!-- Open/close button -->
                                    <xf:var name="fb-vars" value="xxf:instance('fb-variables')"/>
                                    <xf:group appearance="xxf:internal">
                                        <xsl:if test="$collapse">
                                            <!-- Handle DOMActivate event to open/close the switches -->
                                            <xf:action ev:event="DOMActivate">
                                                <xf:var name="expand" value="xxf:case('switch') = 'case-closed'" as="xs:boolean"/>
                                                <xf:action if="$expand">
                                                    <!-- Expand -->
                                                    <xf:setvalue ref="instance('open')">true</xf:setvalue>
                                                </xf:action>
                                                <xf:action if="not($expand)">
                                                    <!-- Collapse -->
                                                    <xf:setvalue ref="instance('open')">false</xf:setvalue>
                                                </xf:action>
                                            </xf:action>
                                        </xsl:if>

                                        <!-- Actual section title -->
                                        <xsl:choose>
                                            <xsl:when test="$collapse">
                                                <xf:trigger appearance="minimal" class="fr-section-label fr-section-{{if ($open) then 'open' else 'closed'}}">
                                                    <xf:label value="xxf:label('fr-section')"/>
                                                    <xf:help  value="xxf:help('fr-section')"/>
                                                    <xf:hint  value="xxf:hint('fr-section')"/>
                                                </xf:trigger>
                                            </xsl:when>
                                            <xsl:otherwise>
                                                <xh:span class="fr-section-label">
                                                    <xf:help  value="xxf:help('fr-section')"  for="switch"/>
                                                    <xf:label value="xxf:label('fr-section')" for="switch"/>
                                                    <xf:hint  value="xxf:hint('fr-section')"  for="switch"/>
                                                </xh:span>
                                            </xsl:otherwise>
                                        </xsl:choose>

                                    </xf:group>

                                </xsl:element>

                                <xf:switch id="switch" xxf:readonly-appearance="dynamic" xxf:element="div">
                                    <!-- Closed section -->
                                    <xf:case id="case-closed" selected="{not($open)}"/>
                                    <!-- Open section -->
                                    <xf:case id="case-open" selected="{$open}">
                                        <xsl:if test="$animate">
                                            <xsl:attribute name="class">xxforms-animate</xsl:attribute>
                                        </xsl:if>
                                        <xf:group class="fr-section-content" xxbl:scope="outer" xxf:element="div">
                                            <!-- Section content except label, event handlers, and buttons -->
                                            <xsl:apply-templates select="* except (xf:label, xf:help, fr:buttons)"/>
                                        </xf:group>
                                    </xf:case>
                                </xf:switch>
                            </xf:group>
                        </xf:group>
                    </xf:group>
                </xsl:template>
            </xsl:transform>
        </xbl:template>
    </xbl:binding>
</xbl:xbl>