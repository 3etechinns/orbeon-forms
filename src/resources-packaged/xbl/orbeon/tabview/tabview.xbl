<?xml version="1.0" encoding="UTF-8"?>
<!--
  Copyright (C) 2009 Orbeon, Inc.

  This program is free software; you can redistribute it and/or modify it under the terms of the
  GNU Lesser General Public License as published by the Free Software Foundation; either version
  2.1 of the License, or (at your option) any later version.

  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
  without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
  See the GNU Lesser General Public License for more details.

  The full text of the license is available at http://www.gnu.org/copyleft/lesser.html
  -->
<xbl:xbl xmlns:xhtml="http://www.w3.org/1999/xhtml"
         xmlns:xforms="http://www.w3.org/2002/xforms"
         xmlns:xs="http://www.w3.org/2001/XMLSchema"
         xmlns:ev="http://www.w3.org/2001/xml-events"
         xmlns:xi="http://www.w3.org/2001/XInclude"
         xmlns:xxi="http://orbeon.org/oxf/xml/xinclude"
         xmlns:xxforms="http://orbeon.org/oxf/xml/xforms"
         xmlns:saxon="http://saxon.sf.net/"
         xmlns:xbl="http://www.w3.org/ns/xbl"
         xmlns:xxbl="http://orbeon.org/oxf/xml/xbl"
         xmlns:fr="http://orbeon.org/oxf/xml/form-runner"
         xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
         xmlns:oxf="http://www.orbeon.com/oxf/processors"
         xmlns:pipeline="java:org.orbeon.oxf.processor.pipeline.PipelineFunctionLibrary">

 	<xbl:script type="text/javascript" src="/ops/yui/element/element-beta.js"/>
    <xbl:script src="/xbl/orbeon/tabview/tabview.js"/>
    <xbl:script src="/ops/yui/tabview/tabview-min.js"/>

    <xbl:binding id="fr-tabview" element="fr|tabview">
        <xbl:resources>
            <xbl:style src="/ops/yui/tabview/assets/skins/sam/tabview.css"/>
        </xbl:resources>
        <xbl:template xxbl:transform="oxf:unsafe-xslt">
            <xsl:transform version="2.0">
                <xsl:import href="oxf:/oxf/xslt/utils/copy.xsl"/>

                <xsl:template match="/*">

                    <!-- Tab group, cannot be internal because accessed with "this" in TabView JS initialization -->
                    <xforms:group id="fr-tabview-group">

                        <!-- TODO: This should not be necessary, but we need it until this bug is fixed:
                             http://forge.ow2.org/tracker/index.php?func=detail&aid=313850&group_id=168&atid=350207 -->
                        <xsl:copy-of select="namespace::*"/>
                        <xsl:copy-of select="@class"/>

                        <!-- Initialization event -->
                        <xxforms:script id="xf-en" ev:event="xforms-enabled" ev:target="fr-tabview-group">YAHOO.xbl.fr.TabView.instance(this).init();</xxforms:script>

                        <xhtml:div class="yui-navset">
                            <xhtml:ul class="yui-nav">
                                <xsl:for-each select="fr:tab">
                                    <xsl:variable name="label" as="element(fr:label)" select="fr:label[1]"/>
                                    <xhtml:li>
                                        <xsl:if test="position() = 1">
                                            <xsl:attribute name="class">selected</xsl:attribute>
                                        </xsl:if>
                                        <!-- Transform xforms:label into xforms:output -->
                                        <xhtml:a href="#tab{position()}"><xhtml:em>
                                            <xsl:choose>
                                                <!-- Static label, just copy content -->
                                                <xsl:when test="empty($label/(@ref | @bind | @value))">
                                                    <xsl:copy-of select="$label/node()"/>
                                                </xsl:when>
                                                <!-- Dynamic label, generate xforms:output -->
                                                <xsl:otherwise>
                                                    <!-- Put content in outer scope -->
                                                    <xforms:output xxbl:scope="outer">
                                                        <xsl:copy-of select="$label/(@model | @context | @ref | @bind | @value | node())"/>
                                                    </xforms:output>
                                                </xsl:otherwise>
                                            </xsl:choose>
                                        </xhtml:em></xhtml:a>
                                    </xhtml:li>
                                </xsl:for-each>
                            </xhtml:ul>
                            <xhtml:div class="yui-content">
                                <xsl:for-each select="fr:tab">
                                    <xsl:variable name="case-id" as="xs:string?" select="@id"/>
                                    <xhtml:div>
                                        <!-- Tab content has outer scope -->
                                        <xforms:group xxbl:scope="outer">
                                            <xsl:if test="$case-id">
                                                <xsl:attribute name="id" select="$case-id"/>
                                                <!-- Custom events -->
                                                <!-- TODO: implement switching -->
                                                <!--<xforms:setvalue ev:event="fr-toggle" ev:target="{$case-id}" ref="operation" xxbl:scope="inner">toggle</xforms:setvalue>-->
                                                <!--<xforms:setvalue ev:event="fr-show" ev:target="{$case-id}" ref="operation" xxbl:scope="inner">show</xforms:setvalue>-->
                                                <!--<xforms:setvalue ev:event="fr-hide" ev:target="{$case-id}" ref="operation" xxbl:scope="inner">hide</xforms:setvalue>-->
                                            </xsl:if>

                                            <!-- Triggers by the JavaScript to notify the XForms code of events -->
                                            <xforms:trigger id="fr-tabview-select-{position()}" class="fr-tabview-select-{position()}" style="display: none" xxbl:scope="inner">
                                                <xforms:label/>
                                                <xforms:dispatch ev:event="DOMActivate" name="fr-select" target="fr-tabview-select-{position()}"/>
                                            </xforms:trigger>
                                            <xforms:trigger id="fr-tabview-deselect-{position()}" class="fr-tabview-deselect-{position()}" style="display: none" xxbl:scope="inner">
                                                <xforms:label/>
                                                <xforms:dispatch ev:event="DOMActivate" name="fr-deselect" target="fr-tabview-deselect-{position()}"/>
                                            </xforms:trigger>

                                            <xsl:apply-templates select="node() except fr:label"/>
                                        </xforms:group>
                                    </xhtml:div>
                                </xsl:for-each>
                            </xhtml:div>
                        </xhtml:div>
                    </xforms:group>
                </xsl:template>
            </xsl:transform>
        </xbl:template>
    </xbl:binding>
</xbl:xbl>
