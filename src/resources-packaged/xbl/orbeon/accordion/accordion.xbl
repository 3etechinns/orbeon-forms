<?xml version="1.0" encoding="UTF-8"?>
<!--
  Copyright (C) 2009 Orbeon, Inc.

  This program is free software; you can redistribute it and/or modify it under the terms of the
  GNU Lesser General Public License as published by the Free Software Foundation; either version
  2.1 of the License, or (at your option) any later version.

  This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
  without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
  See the GNU Lesser General Public License for more details.

  The full text of the license is available at http://www.gnu.org/copyleft/lesser.html
  -->
<xbl:xbl xmlns:xhtml="http://www.w3.org/1999/xhtml"
         xmlns:xforms="http://www.w3.org/2002/xforms"
         xmlns:xs="http://www.w3.org/2001/XMLSchema"
         xmlns:ev="http://www.w3.org/2001/xml-events"
         xmlns:xi="http://www.w3.org/2001/XInclude"
         xmlns:xxi="http://orbeon.org/oxf/xml/xinclude"
         xmlns:xxforms="http://orbeon.org/oxf/xml/xforms"
         xmlns:saxon="http://saxon.sf.net/"
         xmlns:xbl="http://www.w3.org/ns/xbl"
         xmlns:xxbl="http://orbeon.org/oxf/xml/xbl"
         xmlns:fr="http://orbeon.org/oxf/xml/form-runner"
         xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
         xmlns:oxf="http://www.orbeon.com/oxf/processors"
         xmlns:pipeline="java:org.orbeon.oxf.processor.pipeline.PipelineFunctionLibrary">

    <xbl:script src="/xbl/orbeon/accordion/accordion-menu-v2.js"/>
    <xbl:script src="/xbl/orbeon/accordion/accordion.js"/>

    <xbl:binding id="fr-accordion" element="fr|accordion">
        <xbl:resources>
            <xbl:style src="/xbl/orbeon/accordion/accordion.css"/>
        </xbl:resources>
        <xbl:handlers>
            <xbl:handler event="fr-accordion-toggle-all" phase="target">
                <xforms:setvalue model="accordion-model" ref="selected" value="event('selected')"/>
                <xxforms:script id="xf-ta">YAHOO.xbl.fr.Accordion.toggleAll(this);</xxforms:script>
            </xbl:handler>
            <!-- Legacy method to toggle a given case. Prefer dispatching fr-toggle directly to fr:case by id. -->
            <xbl:handler event="fr-accordion-toggle" phase="target" xxbl:scope="outer">
                <xforms:dispatch target="{event('case-id')}" name="fr-accordion-toggle">
                    <xxforms:context name="selected" select="event('selected')"/>
                </xforms:dispatch>
            </xbl:handler>
        </xbl:handlers>
        <xbl:implementation>
            <!-- Local model -->
            <xforms:model id="accordion-model">
                <xforms:instance id="accordion-instance">
                    <instance>
                        <selected/>
                    </instance>
                </xforms:instance>
            </xforms:model>
        </xbl:implementation>
        <xbl:template xxbl:transform="oxf:unsafe-xslt">
            <xsl:transform version="2.0">
                <xsl:import href="oxf:/oxf/xslt/utils/copy.xsl"/>
                <xsl:import href="oxf:/oxf/xslt/utils/xbl.xsl"/>

                <xsl:template match="/*">
                    <xforms:group ref="." id="fr-accordion-group">

                        <!-- TODO: This should not be necessary, but we need it until this bug is fixed:
                             http://forge.ow2.org/tracker/index.php?func=detail&aid=313850&group_id=168&atid=350207 -->
                        <xsl:copy-of select="namespace::*"/>
                        <xsl:copy-of select="@class"/>

                        <!-- Access to parameters -->
                        <xsl:copy-of select="xxbl:parameter(., 'fr', 'http://orbeon.org/oxf/xml/form-runner', 'accordion', 'open-closes-others')"/>

                        <!-- Event handlers -->
                        <xxforms:script id="xf-en" ev:event="xforms-enabled" ev:target="fr-accordion-group">YAHOO.xbl.fr.Accordion.init(this);</xxforms:script>
                        <xforms:action ev:event="fr-accordion-toggle">
                            <xxforms:script>YAHOO.xbl.fr.Accordion.toggle(arguments[0].target);</xxforms:script>
                        </xforms:action>

                        <!-- Dummy controls so JavaScript can access the local instance -->
                        <xforms:output model="accordion-model" value="selected" class="xbl-fr-accordion-selected" style="display: none"/>

                        <!-- Markup for the accordion -->
                        <xhtml:dl id="dl" class="xbl-fr-accordion-dl">
                            <xsl:apply-templates/>
                        </xhtml:dl>
                    </xforms:group>
                </xsl:template>

                <!-- Placed enclosing repeas and group in outer scope -->
                <xsl:template match="/*/xforms:repeat | /*/xforms:group">
                    <xsl:copy>
                        <xsl:copy-of select="@*"/>
                        <xsl:attribute name="xxbl:scope">outer</xsl:attribute>
                        <xsl:apply-templates/>
                    </xsl:copy>
                </xsl:template>

                <xsl:template match="/*/fr:case | /*/xforms:repeat/fr:case | /*/xforms:group/fr:case">
                    <!--<xsl:variable name="case-id" as="xs:string" select="if (@id) then @id else generate-id()"/>-->
                    <xsl:variable name="case-id" as="xs:string?" select="@id"/>
                    <xsl:variable name="label" as="element(fr:label)" select="fr:label[1]"/>
                    <!-- Section title -->
                    <!--<xhtml:dt id="t-{$case-id}" class="a-m-t{ if (@selected = 'true') then ' a-m-t-expand' else ''}">-->
                    <xhtml:dt class="a-m-t{ if (@selected = 'true') then ' a-m-t-expand' else ''}">
                        <xsl:if test="$case-id"><xsl:attribute name="id" select="concat('t-', $case-id)"/></xsl:if>
                        <xsl:choose>
                            <!-- Static label, just copy content -->
                            <xsl:when test="empty(($label/@ref, $label/@bind, $label/@value))">
                                <xsl:copy-of select="$label/node()"/>
                            </xsl:when>
                            <!-- Dynamic label, generate xforms:output -->
                            <xsl:otherwise>
                                <!-- Put content in outer scope -->
                                <xforms:output xxbl:scope="outer">
                                    <xsl:copy-of select="$label/@* | $label/node()"/>
                                </xforms:output>
                            </xsl:otherwise>
                        </xsl:choose>
                    </xhtml:dt>
                    <!-- Section body -->
                    <!--<xhtml:dd id="d-{$case-id}" class="a-m-d{ if (@selected = 'true') then ' a-m-d-expand' else ''}">-->
                    <xhtml:dd class="a-m-d{ if (@selected = 'true') then ' a-m-d-expand' else ''}">
                        <xsl:if test="$case-id"><xsl:attribute name="id" select="concat('d-', $case-id)"/></xsl:if>
                        <xhtml:div class="bd">
                            <!-- Put content in outer scope -->
                            <xforms:group xxbl:scope="outer">
                                <xsl:if test="$case-id">
                                    <xsl:attribute name="id" select="$case-id"/>
                                    <!-- Custom toggling event -->
                                    <xforms:action ev:event="fr-accordion-toggle" ev:target="{$case-id}">
                                        <xforms:setvalue model="accordion-model" ref="selected" value="event('selected')" xxbl:scope="inner"/>
                                    </xforms:action>
                                </xsl:if>

                                <xsl:copy-of select="node() except $label"/>
                            </xforms:group>
                        </xhtml:div>
                    </xhtml:dd>
                </xsl:template>

            </xsl:transform>
        </xbl:template>
    </xbl:binding>
</xbl:xbl>
