<!--
    Copyright (C) 2006 Orbeon, Inc.

    This program is free software; you can redistribute it and/or modify it under the terms of the
    GNU Lesser General Public License as published by the Free Software Foundation; either version
    2.1 of the License, or (at your option) any later version.

    This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
    without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
    See the GNU Lesser General Public License for more details.

    The full text of the license is available at http://www.gnu.org/copyleft/lesser.html
-->
<xsl:stylesheet version="2.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
        xmlns:xforms="http://www.w3.org/2002/xforms"
        xmlns:xxforms="http://orbeon.org/oxf/xml/xforms"
        xmlns:f="http://orbeon.org/function"
        xmlns:xs="http://www.w3.org/2001/XMLSchema"
        xmlns:xi="http://www.w3.org/2001/XInclude"
        xmlns:ev="http://www.w3.org/2001/xml-events"
        xmlns:xhtml="http://www.w3.org/1999/xhtml">

    <xsl:template match="/">
        <xhtml:html>
            <xhtml:head>
                <xhtml:title>XForms Sortable Table</xhtml:title>
                <xforms:model>
                    <xforms:instance>
                        <instance>
                            <!-- Sorting information -->
                            <sort-by>name</sort-by>
                            <sort-order>ascending</sort-order>
                            <sort-data-type>text</sort-data-type>
                            <!-- Countries data -->
                            <xi:include href="data.xml"/>
                        </instance>
                    </xforms:instance>
                    <xforms:submission method="post" id="sort" action="/direct/xforms-report/sort" replace="instance"/>
                </xforms:model>
                <xhtml:style type="text/css">
                    .xforms-repeat-selected-item-1 { background: none; }
                </xhtml:style>
            </xhtml:head>
            <xhtml:body>
                <!-- Data table -->
                <xhtml:table class="gridtable">
                    <xhtml:tr>
                        <xsl:copy-of select="f:column('Name', 'name', 'text')"/>
                        <xsl:copy-of select="f:column('Capital', 'capital', 'text')"/>
                        <xsl:copy-of select="f:column('Population', 'population', 'number')"/>
                        <xsl:copy-of select="f:column('GDP', 'gdp', 'number')"/>
                    </xhtml:tr>
                    <xforms:repeat nodeset="/instance/countries/country">
                        <xhtml:tr>
                            <xhtml:td>
                                <xforms:output ref="name"/>
                            </xhtml:td>
                            <xhtml:td>
                                <xforms:output ref="capital"/>
                            </xhtml:td>
                            <xhtml:td>
                                <xforms:output ref="population"/>
                            </xhtml:td>
                            <xhtml:td>
                                <xforms:output ref="gdp"/>
                            </xhtml:td>
                        </xhtml:tr>
                    </xforms:repeat>
                    <xhtml:tr/>
                </xhtml:table>
            </xhtml:body>
        </xhtml:html>
    </xsl:template>

    <!-- Display column header -->
    <xsl:function name="f:column" as="element(xhtml:th)">
        <xsl:param name="label" as="xs:string"/>
        <xsl:param name="value" as="xs:string"/>
        <xsl:param name="data-type" as="xs:string"/>

        <xhtml:th>
            <xsl:value-of select="$label"/>
            <xforms:group ref=".[sort-by = '{$value}']">
                <xforms:group ref=".[sort-order = 'ascending']">
                    <!-- We are in ascending order -->
                    <xforms:trigger appearance="xxforms:image">
                        <xxforms:img src="/images/sort-down.gif"/>
                        <xforms:label/>
                        <xforms:action ev:event="DOMActivate">
                            <xforms:setvalue ref="sort-order">descending</xforms:setvalue>
                            <xforms:send submission="sort"/>
                        </xforms:action>
                    </xforms:trigger>
                </xforms:group>
                <xforms:group ref=".[sort-order = 'descending']">
                    <!-- We are in descending order -->
                    <xforms:trigger appearance="xxforms:image">
                        <xxforms:img src="/images/sort-up.gif"/>
                        <xforms:label/>
                        <xforms:action ev:event="DOMActivate">
                            <xforms:setvalue ref="sort-order">ascending</xforms:setvalue>
                            <xforms:send submission="sort"/>
                        </xforms:action>
                    </xforms:trigger>
                </xforms:group>
            </xforms:group>
            <xforms:group ref=".[sort-by != '{$value}']">
                <!-- Not sorted by this column -->
                <xforms:trigger appearance="xxforms:image">
                    <xxforms:img src="/images/sort-natural.gif"/>
                    <xforms:label/>
                    <xforms:action ev:event="DOMActivate">
                        <xforms:setvalue ref="sort-order">ascending</xforms:setvalue>
                        <xforms:setvalue ref="sort-data-type"><xsl:value-of select="$data-type"/></xforms:setvalue>
                        <xforms:setvalue ref="sort-by"><xsl:value-of select="$value"/></xforms:setvalue>
                        <xforms:send submission="sort"/>
                    </xforms:action>
                </xforms:trigger>
            </xforms:group>
        </xhtml:th>
    </xsl:function>

</xsl:stylesheet>
